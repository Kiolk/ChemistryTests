apply plugin: 'com.android.application'

apply plugin: 'io.fabric'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply from : 'dependencies.gradle'

def propsNote = new Properties()
file("releaseNote.properties").withInputStream{propsNote.load(it)}

android {

    compileSdkVersion 26
    defaultConfig {
        applicationId "com.github.kiolk.chemistrytests"
        minSdkVersion 16
        targetSdkVersion 26
        multiDexEnabled true
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ext.betaDistributionReleaseNotes=propsNote.getProperty("releaseNote")
        ext.betaDistributionEmails=propsNote.getProperty("distributionEmails")
        ext.betaDistributionGroupAliases=propsNote.getProperty("groupAliases")
    }

    signingConfigs {
        release {
            storeFile file(keyPath)
            storePassword keyStorePassword
            keyAlias keyAliath
            keyPassword keyPasswordSign
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.release
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    applicationVariants.all{variant ->
        variant.outputs.all {
            def buildType = variant.buildType.debuggable ? "debug" : "release"
            outputFileName = "ChemistryTest-${android.defaultConfig.versionCode}-v${android.defaultConfig.versionName}-${buildType}.apk"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation library.appCompat
    implementation library.supportDesign
    implementation library.fireBaseCore
    implementation library.firebaseDatabase
    implementation library.firebaseMessagin
    implementation library.firebaseAuth
    implementation library.gson
    implementation library.firebaseUiAuth
    implementation library.constariantLayout
    implementation library.benzeneBar
    implementation library.cardView
    implementation library.floatAB
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation project(':pen')
    implementation 'com.google.android.gms:play-services:12.0.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.6@aar') {
        transitive = true;
    }
}


apply plugin: 'com.google.gms.google-services'